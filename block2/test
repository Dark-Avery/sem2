@echo off
REM  Отключили отображение на экран выполняемых команд этого файла
REM  =========================================================================
REM  | Это коммандный файл m.bat с доопределением переменной окружения path  |
REM  =========================================================================
REM Проверка наличия параметра при вызове коммандного файла m.bat :
REM %1   - первый (и единственный) параметр, 
REM %~n1 - имя файла, переданного как параметр, без расширения
if .%1.==.. goto USAGE
if exist %1 goto start

:USAGE
REM сообщение о том, что при запуске надо указывать
REM  имя компилируемого файла с расширением  
echo usage: m program.asm
  goto TheEnd
 
:start 
REM указание мест, где следует искать дистрибутив MASM:
  set path= %path%;c:\masm 6.14\bin;c:\masm32\bin;..\bin;..\..\bin
REM  c:\masm 6.14 - на факультете,  c:\masm32 - дома 
  
REM Параметры (компиляции) ассемблера (ml.exe -?):
REM    /Fl     создание листинга (%~n1.lst)
REM    /I...   каталог с включаемыми (по include) файлами
REM    /c      трансляция в объектный файл (%~n1.obj), без компоновки
REM    /coff   формат объектного файла — COFF (Common Object File Format)
REM    /nologo запрет выдавать на экран версию компилятора и др.
REM -------------------------------------------------------------------------------
ml.exe /Fl /Ic:\masm32\include /I"c:\tools\masm 6.14\include" /c /coff /nologo %1
REM -------------------------------------------------------------------------------
REM Проверка успешности компиляции
if errorlevel 1 goto errasm

REM Параметры компоновки (link.exe -?):
REM    /libpath:...       каталог с вспомогательными библиотеками
REM    /subsystem:console указание строить консольное Windows-приложение.
REM    /nologo            запрет выдавать на экран версию редактора связей и др.
REM --------------------------------------------------------------------------------------------
link.exe /libpath:"c:\masm 6.14\lib" /libpath:c:\masm32\lib /subsystem:console /nologo %~n1.obj 
REM --------------------------------------------------------------------------------------------
REM  Удаление промежуточного объектного файла %~n1.obj
del %~n1.obj 
  goto TheEnd

:errasm
REM Сообщение о том, что в процессе компиляции были зафиксированы ошибки
  echo Assembler Error !!!!!!!!!!!!
  goto TheEnd

:TheEnd
rem pause
rem %~n1.exe
rem echo on
include console.inc

.data
T   db " Зянчурин Игорь (группа 110)     "   ; информация об авторе программы
    db " Задача_0 (блок_0) 'Название' ",0       ; информация о решаемой задаче

.code
    Start:

        ConsoleTitle offset T

        
        pause
        exit
    end Start
