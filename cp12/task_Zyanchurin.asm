include console.inc

COMMENT ~

САМОСТОЯТЕЛЬНАЯ РАБОТА 12
СТУДЕНТ Зянчурин Игорь     группа 110

~

COMMENT~ 
----------------------------------------------------------------------------
ВНИМАНИЕ: ВСЕ ИСПОЛЬЗУЕМЫЕ ОПИСАНИЯ ТИПОВ И ПЕРЕМЕННЫХ ДАЮТСЯ В СЕКЦИИ
ДАННЫХ ЭТОЙ ПРОГРАММЫ !!!
----------------------------------------------------------------------------
ЗАДАЧИ ДЕЛАТЬ ТОЛЬКО В УКАЗАННОЙ ПОСЛЕДОВАТЕЛЬНОСТИ !!!
---------------------------------------------------------------------------- 
 ЗАДАЧА_1 (максимально 25 очков)
 (СТАНДАРТНЫЕ СОГЛАШЕНИЯ О СВЯЗЯХ stdcall) 
 ПЕРЕДАЧА ПАРАМЕТРА ПО ССЫЛКЕ
 
 Описать процедуру In_struc(Str), где Str - обозначение переменной-структуры
 типа TS (описание типа дано ниже в секции данных). Процедура вводит (по inint) 
 значения для полей min и sec этой  структуры. Считать, что эти значения будут
 введены корректно и не выйдут из диапазона [0..59]. Обеспечить соблюдение 
 стандартных соглашений о связях. Не надо выводить приглашения к вводу в процессе 
 работы процедуры (и в основной программе тоже не надо выводить такие приглашения)!
  
 Применить эту процедуру к переменной-структуре S для ввода значений полей этой 
 структуры (передав адрес этой переменной в качестве параметра). Вывести (из 
 основной программы, т.е. извне процедуры) полученные значения полей структуры S,
 чтобы убедиться в правильности работы написанной процедуры.
----------------------------------------------------------------------------
 ЗАДАЧА_2 (максимально 35 очков)
 (СТАНДАРТНЫЕ СОГЛАШЕНИЯ О СВЯЗЯХ stdcall) 
 ПЕРЕДАЧА ПАРАМЕТРОВ ПО ССЫЛКЕ
 
 Описать процедуру Struc_to_Record(Str,Rec), где Str - обозначение 
 переменной-структуры типа TS, Rec - обозначение переменной-записи типа TR. 
 Процедура заполняет поля min и sec записи Rec значениями полей min и sec
 структуры Str. Считать, что значения полей структуры заданы корректно и не
 выйдут из диапазона [0..59]. Выполнить стандартные соглашения о связях.
  
 Применить эту процедуру к переменной-структуре S и переменной-записи R
 (передав адреса этих переменных в качестве параметров).
 Вывести полученные значения полей записи Rec (из основной программы, т.е. 
 извне процедуры), чтобы убедиться в правильности работы процедуры).
----------------------------------------------------------------------------
 ЗАДАЧА_3 (максимально 25 очков)
 (СТАНДАРТНЫЕ СОГЛАШЕНИЯ О СВЯЗЯХ stdcall) 
 ПЕРЕДАЧА ПАРАМЕТРА ПО-ЗНАЧЕНИЮ
 
 Описать функцию Seconds(Rec), где Rec - обозначение переменной-записи 
 типа TR. Переменная Rec содержит некоторый момент времени в рамках текущего
 часа - минуту (в поле min) и секунду (в поле sec). Функция вычисляет полное
 количество секунд, прошедших с начала часа, и возвращает ответ через АХ.
 Выполнить стандартные соглашения о связях.
  
 Применить эту функцию к переменной-записи R (параметр передать по-значению),
 поля которой были заполнены  при решении ЗАДАЧИ_2. Вывести (из основной 
 программы) полученный ответ, чтобы убедиться в правильности работы функции).
----------------------------------------------------------------------------
~

.data
T   db " Зянчурин Игорь 110   Задача (с-р 12)",0	;	

; ------------ ОПИСАНИЕ ТИПОВ и ПЕРЕМЕННЫХ для ЗАДАЧ 1-3: ------------------
TS  struc
    min  db ?     ; [0..59]
    sec  db ?     ; [0..59]
TS ends

S   TS  <>

TR  record min:6, sec:6

R  TR   <>

.code
; --------------------------------------------------------------------------
;              ЗАГОТОВКИ ОПИСАНИЯ ПРОЦЕДУР для ЗАДАЧ 1-3: 
; --------------------------------------------------------------------------
; procedure In_struc(var S: TS)
In_struc proc
; stdcall, в стеке параметр:
; адрес структуры типа TS
; ДЕЙСТВИЕ:
; Ввод полей min и sec структуры типа TS:
    push EBP
    mov EBP, ESP
    push EBX
    
    mov EBX, [EBP+8]
    inint [EBX].TS.min
    inint [EBX].TS.sec
    
    pop EBX
    pop EBP
    ret 4
In_struc endp
;--------------------------------------------------------------------------
; procedure Struc_to_Record(var Str: TS; var Rec: TR)
Struc_to_Record proc
; stdcall, в стеке параметры (сверху вниз): 
; адрес структуры типа TS   
; адрес записи типа TR
; ДЕЙСТВИЕ:
; Заполнение полей min и sec записи Rec по полям min и sec структуры типа TS:
; СОВЕТ по решению: сформировать значение искомой записи на регистре АХ, и 
; только затем полученное значение записать в память
    push EBP
    mov EBP, ESP
    push EAX
    push EBX
    push ECX
    
    mov ECX, [EBP+8]
    movzx AX, [ECX].TS.min
    shl AX, 6
    movzx BX, [ECX].TS.sec
    or AX, BX
    mov ECX, [EBP+12]
    mov [ECX], AX
    
    pop ECX
    pop EBX
    pop EAX
    pop EBP
    ret 4*2
Struc_to_Record endp
;---------------------------------------------------------------------------
; function Seconds(Rec: TR):word
Seconds proc
; stdcall, в стеке параметр: 
; копия (значения) записи типа TR (в формате двойного слова, т.к. stdcall)
; ДЕЙСТВИЕ:
; Вычисление АХ := min*60+sec (где min и sec - содержимое полей записи)
    push EBP
    mov EBP, ESP
    push EBX
    push EDX
    
    mov EBX, [EBP+8]
    mov EAX, EBX
    shr EAX, 6
    mov EDX, 60
    mul DL
    and EBX, mask sec
    add AX, BX
    
    pop EDX
    pop EBX
    pop EBP
    ret 4
Seconds endp
;---------------------------------------------------------------------------

Start:
    ConsoleTitle offset T	
    
;-------------- ЗАДАЧА 1 -----------------------------------------------
; применение процедуры In_struc к переменной S типа TS:
    push offset S
    call In_struc
; проверка правильности работы процедуры
; (т.е. печать значений полей min и sec структуры S):
    outintln S.min,,'S.min = '
    outintln S.sec,,'S.sec = '
;-------------- ЗАДАЧА 2 -----------------------------------------------
; применение процедуры Struc_to_Record к структуре S типа TS и записи R типа TR:
    push offset R
    push offset S
    call Struc_to_Record
; проверка правильности работы процедуры
; (т.е. печать значений полей min и sec записи R): 
    mov AX, R
    shr AX, min
    outintln AL,,'R.min = '
    mov AX, R
    and AX, mask sec
    outintln AL,,'R.sec = '
;-------------- ЗАДАЧА 3 -----------------------------------------------
; применение функции Seconds к записи R типа TR:
    movzx EBX, R
    push EBX
    call Seconds
; вывод найденного ответа:
    outintln AX,,'total_seconds = '
;------------------------------------------------------------------------ 
        
    pause						; нужно при сдаче по e-mail
    exit						
    end Start
    
COMMENT~

для тестирования:
min=45, sec=55, total_seconds=2755
min=0,  sec=54, total_seconds=54
min=59, sec=59, total_seconds=3599

~

